(define (problem move_small_objects_to_a)
  ; Shakey's task is to retrieve all small objects to room a,
  ; and turn lights off in all other rooms when it is done.
  (:domain shakey)
  (:objects rooma roomb roomc switcha switchb switchc boxa wideab widebc narrowbc left right blockx blocky blockz)
  ; There are three rooms (rooma, roomb, roomc), which are connected by doors, wideab (between rooma and roomb),
  ; wide bc (between roomb and roomc) and narrowbc (between roomb and roomc).
  ; In each room there is a lightswitch (switcha, switchb and switchc).
  ; There is a large box, boxa, in roomc. There are three small objects, blockx (in roomb),
  ; blocky and blockz (in roomc). Shakey has two grippers, left and right.
  (:init (room rooma)
          (room roomb)
	  (room roomc)
	  (door wideab)
	  (door widebc)
	  (door narrowbc)
	  (small-object blockx)
	  (small-object blocky)
	  (small-object blockz)
	  (lightswitch switcha)
	  (lightswitch switchb)
	  (lightswitch switchc)
	  (box boxa)
          (gripper left)
          (gripper right)
          (at-shakey rooma)
          (free left)
          (free right)
	  (wide wideab)
	  (wide widebc)
	  (door-between wideab rooma roomb)
	  (door-between wideab roomb rooma)
	  (door-between widebc roomb roomc)
	  (door-between widebc roomc roomb)
	  (door-between narrowbc roomb roomc)
	  (door-between narrowbc roomc roomb)
	  (at boxa roomc)
	  (at switcha rooma)
          (at switchb roomb)
	  (at switchc roomc)
	  (at blockx roomb)
	  (at blocky roomc)
	  (at blockz roomc)
	  )
   (:goal (and (lightswitch-on switcha) (not (lightswitch-on switchb)) (not (lightswitch-on switchc))
	       (at blockx rooma) (at blocky rooma) (at blockz rooma) (at-shakey rooma)
          )
   )
)